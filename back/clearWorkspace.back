(
  { noop 1 2 3 4 5 6 print print print print print print } "test1" store { noop a b c d e f print print print print print print } "test2" store
  test1 "test1" store test2 "test2" store

  ( { 1 2 3 4 5 6 print print print print print print } { a b c d e f print print print print print print } )
  slide ( swap slip 1 sP )


  { |B { |B 1 2 3 4 5 6 print print print print print print } { |B a b c d e f print print print print print print } }
  0 "cc" store ( cc 1 + dup "cc" store 36 < ) ( slip ) while cc print

) "cw_test" store


(
  pointToClearanceMap aboutFace currentPose cw_clearWorkspace
) "cw_sweepToGreen" store

(
  /* warning, still hardcoded for playworkspace */
  moveEeToPoseWord waitUntilAtCurrentPosition
  1 changeToHeight

  cw_viewIsClear
) "cw_workspaceIsClear" store

0.3 "cw_viewIsClear_discrepancyThresh" store
500 "cw_viewIsClear_numCellThresh" store
( 
  /* warning, still hardcoded for playworkspace */
  /* if you are in blocks, you will end up back in blocks. if not you will get whatever you had. */
  tableRestoreFromBlocks
  tableSwapToBlocks

  setMovementStateToMoving comeToStop 
  sceneClearObservedMap sceneClearPredictedObjects

  clearClassLabels
  initializeAndFocusOnTempClass

  /* based on tableRecaptureServoSpiral */
  sceneClearPredictedObjects
  sceneClearObservedMap catScan5PlayTakeScene tableUpdateMaps

  "cw_viewIsClear dT nCT: " cw_viewIsClear_discrepancyThresh " " cw_viewIsClear_numCellThresh + + + print

      cw_viewIsClear_discrepancyThresh 
    sceneCountDiscrepantCells
    "cw_viewIsClear_numDiscrepantCells"
  store
  

    cw_viewIsClear_numDiscrepantCells
    " cells are discrepant" 
  + print 

  tableCaptureCore
  tableUpdateMaps

  tableServoToFocusedSceneObject
  /* end based on */


/*
  scenePushNumSceneObjects 0 = 
*/

    cw_viewIsClear_numDiscrepantCells
    cw_viewIsClear_numCellThresh
  >
  ( 
    "NO, view is NOT CLEAR" print
    0 
  ) (
    "YES, view is CLEAR" print
    1 
    sceneClearPredictedObjects
  ) ifte

  tableRestoreFromBlocks
) "cw_viewIsClear" store

( 
  setMovementStateToMoving comeToStop visionCycleNoClassify
  numBlueBoxes 0 = 1 0 ifte
) "cw_viewIsClear_old" store

(
  tableSpiralServoToAnonymousObject
  shiftIntoGraspGear1
) "cw_servo_for_flick" store

(
  1 waitSetCurrentWaitMode

  halfImpulse 
  moveEeToPoseWord 

  0.01 setGridSize 
  ( 
    localXUp localZUp 
    endStackCollapseNoop
  ) 20 replicateWord 
  waitUntilAtCurrentPosition 
  comeToStop
  pressUntilEffortInit 0.03 setSpeed pressUntilEffortCombo

  tenthImpulse 
  0.01 setGridSize 
  ( 
    localXDown 
    endStackCollapseNoop
  ) 40 replicateWord 
  ( 
    closeGripper waitUntilGripperNotMoving 
    isGripperGripping ! ( openGripper waitUntilGripperNotMoving ) ift
  ) 3 replicateWord

  waitUntilAtCurrentPosition
  liftArm waitUntilAtCurrentPosition
  currentPose "cw_flick_returnpoint" store

  isGripperGripping (
    tenthImpulse
    outputPileWorkspace moveEeToPoseWord 0 changeToHeight waitUntilAtCurrentPosition comeToStop
    pressUntilEffortInit 0.05 setSpeed pressUntilEffort openGripper waitUntilGripperNotMoving
    liftArm waitUntilAtCurrentPosition
    tenthImpulse
    cw_flick_returnpoint moveEeToPoseWord waitUntilAtCurrentPosition
  ) (
    openGripper waitUntilGripperNotMoving
  ) ifte

  0 waitSetCurrentWaitMode
) "cw_flick" store

(
  cw_servo_for_flick
  currentPose

  dup
  moveEeToPoseWord 0.01 setGridSize ( yUp ) 4 replicateWord currentPose
  cw_flick

  dup
  cw_flick

  dup
  moveEeToPoseWord 0.01 setGridSize ( yDown ) 4 replicateWord currentPose
  cw_flick

  pop
) "cw_tri_flick" store

3 "cw_clearWorkspace_max" store
(
  tableSwapToBlocks
  catScan5LoadPlayBg

  1 setCurrentIKBoundaryMode
  ">>>> clearing workspace scene <<<<" print

  0 "cw_clearWorkspace_counter" store
  1 "cw_clearWorkspace_rescan" store
  ( 
    cw_clearWorkspace_counter cw_clearWorkspace_max < 
  ) (
      dup
    moveEeToPoseWord waitUntilAtCurrentPosition
    1 changeToHeight

    sceneClearPredictedObjects 
  
    "Attablet " cw_clearWorkspace_counter " out of " cw_clearWorkspace_max + + + print

      dup /* copy the workspace pose */
    cw_workspaceIsClear "cw_clearWorkspace_space_clear" store

    cw_clearWorkspace_space_clear (
      "Great! Workspace clear, moving on." print
      cw_clearWorkspace_max "cw_clearWorkspace_counter" store
      0 "cw_clearWorkspace_rescan" store
    ) (
      "Workspace not clear, attableting to flick object." print
      
      /* should already be at the right place thanks to cw_workspaceIsClear */
      /* 
      tableSpiralServoToAnonymousObject 
      */
      waitUntilAtCurrentPosition
      cw_tri_flick

      cw_clearWorkspace_counter 1 + "cw_clearWorkspace_counter" store
    ) ifte
  ) while


  cw_clearWorkspace_rescan (
    "Sadly, we timed out and the workspace is not clear. Updating the play workspace background map." print
    catScan5UpdatePlayBg
  ) ift
  
  tableRestoreFromBlocks

  print
) "cw_clearWorkspace" store








